<Programa> ::= <Elemento>*
<Elemento> ::= <Personagem> | <Cenario> | <Acao> | <Item> | <Variavel> | <Escolha>

<Personagem> ::= PERSONAGEM STRING LBRACE 
                "classe" COLON STRING SEMICOLON 
                "nivel" COLON INT SEMICOLON 
                "atributos" COLON <ListaAtributos> SEMICOLON 
                RBRACE

<ListaAtributos> ::= <Atributo> (COMMA <Atributo>)*
<Atributo> ::= STRING INT

<Cenario> ::= CENARIO STRING LBRACE 
            "descricao" COLON STRING SEMICOLON 
            "itens" COLON <ListaItens>? SEMICOLON 
            RBRACE

<ListaItens> ::= <Item> (COMMA <Item>)*

<Item> ::= ITEM STRING LBRACE
            "descricao" COLON STRING SEMICOLON
            "tipo" COLON STRING SEMICOLON
            RBRACE

<Acao> ::= ACAO STRING LBRACE 
            "descricao" COLON STRING SEMICOLON 
            "resultado" COLON INT SEMICOLON 
            RBRACE

<Variavel> ::= VARIAVEL STRING ASSIGN <Valor>
<Valor> ::= STRING | INT | FLOAT | BOOLEAN

<Escolha> ::= ESCOLHA LBRACE <Opcao>* RBRACE
<Opcao> ::= OPCAO STRING COLON LBRACE 
            "texto" COLON STRING SEMICOLON 
            "acao" COLON <ElementoAcao> SEMICOLON 
            "condicao" COLON <Condicao>? SEMICOLON 
            RBRACE

<ElementoAcao> ::= <Acao> (COMMA <ModificaVariavel>)*
<ModificaVariavel> ::= VARIAVEL STRING ASSIGN <Valor> SEMICOLON

<Condicao> ::= STRING COMPARADOR <Valor> ("AND" <Condicao> | "OR" <Condicao>)?

<Comparador> ::= "==" | "!=" | "<" | ">" | "<=" | ">="


